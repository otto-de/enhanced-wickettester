apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

version = '0.3-SNAPSHOT'

sourceCompatibility = 1.7

group = 'de.otto'

repositories {
    mavenCentral()

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'org.apache.wicket:wicket-core:7.0.0'
    compile 'commons-lang:commons-lang:2.6'
    compile 'org.hamcrest:hamcrest-all:1.3'
    compile 'org.testng:testng:6.1.1'
}

jar {
    manifest.attributes provider: 'gradle'
}

test {
    useTestNG()
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives packageJavadoc
    archives packageSources
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.version = "0.2"
            pom.artifactId = "enhanced-wickettester"
            pom.groupId = "de.otto"
            pom.project {
                name 'enhanced-wickettester'
                packaging 'jar'
                description 'A enhanced wickettester library'
                url 'http://github.com/otto-de/enhanced-wickettester'

                scm {
                    url 'scm:git@github.com:otto-de/enhanced-wickettester.git'
                    connection 'scm:git@github.com:otto-de/enhanced-wickettester.git'
                    developerConnection 'scm:git@github.com:otto-de/enhanced-wickettester.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'sschmidt'
                        name 'Stefan Schmidt'
                    }
                }
            }
        }
    }
}
